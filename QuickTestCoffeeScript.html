<!DOCTYPE html>
<html>
<head>
	<meta charset="utf-8">
	<meta http-equiv="X-UA-Compatible" content="IE=edge">
	<title>Quick Test CoffeeScript</title>
	<link rel="stylesheet" href="lib/CodeMirror/lib/codemirror.css">
	<link rel="stylesheet" href="lib/CodeMirror/theme/elegant.css">
	<link rel="stylesheet" href="lib/Touch-Splitter-jQuery/src/touchsplitter.css">
	<style id="style" type="text/css" media="screen">
	html,body,#code-splitter,.CodeMirror{
		width:100%;
		height:100%;
		margin:0;
	}
	/* Styles that I like in ACE and Sublime */
	.CodeMirror {font-family: 'Monaco', 'Menlo', 'Ubuntu Mono', 'Consolas', 'source-code-pro', monospace;}
	.CodeMirror {line-height: 1.3em}
	.CodeMirror-selected  { background-color: rgba(0,0,0,0.8) !important;}
	.CodeMirror-selectedtext { color: white !important; text-decoration: none !important; font-style: normal !important; font-weight: normal !important;}

	.TouchSplitter.hTS{overflow:visible;}
	.TouchSplitter .splitter-bar{opacity:0;}
	.CodeMirror{box-shadow:0 0 12px rgba(0,0,0,0.2);}

	.CodeMirror:hover{cursor:text;}
	.CodeMirror-gutters:hover{cursor:default;}
	#output-splitter {
		position:absolute;
		top:42px;
		bottom:0;
		left:0;
		right:0;
	}
	.cm-s-solarized .CodeMirror-gutters{box-shadow: none}
	.splitter-bar{
		background-color: #f5f5f5;
		z-index:4;
		position:relative;
		box-sizing:border-box;
	}

	.error, .title, .action{font-family: "Helvetica Neue", "Lucida Grande", "Lucida Sans Unicode", Helvetica, Arial, sans-serif !important;}
	.header{width:100%;height:42px; position:relative; background-color: #13ACD3;}
	.action, .title {line-height:32px;padding:5px 33px;height:32px;float:left;color:white;margin-right: 2px}
	.title {font-size: 19px; color:white;}
	.action {background:rgba(0,0,0,.2);}
	.action:hover {cursor: pointer}
	.error {position:absolute; right:0;top:0;line-height:42px;padding:0 12px;background: #ff756f}
	.code {font-family: monospace;}

	.dropdown {
		position:relative;
		float:left;
	}
	.dropdown:hover ul {
		z-index: 5;
		display:block;
	}
	.dropdown ul {
		position:absolute;
		display:none;
		list-style:none;
		margin:0;
		padding:0;
		top: 42px;
	}
	.dropdown ul li {background-color: #13ACD3;padding: 10px;width: 190px;}
	.dropdown ul li input {width:150px;padding:1px;margin:1px;}
	.dropdown ul li span {font-weight:bold;cursor:pointer;margin:0;padding:10px;width:16px;}
	</style>
</head>
<body>
	<div class="header">
		<div class="title">Try CoffeeScript</div>
		<div class="action" onclick="editor.run()">Run</div>
		<div class="action" onclick="editor.refresh()">Refresh</div>
		<div class="dropdown">
			<span class="action">Add Library</span>
			<ul>
				<li><input type="text" placeholder="Add js/coffee Library URL"></input><span onclick="editor.addLibrary(this)">+</span></li>
			</ul>
		</div>
		<div class="action" onclick="editor.share()">Share</div>
		<div class="error" id="error"></div>
	</div>
	<div id="output-splitter">
		<div id="code-splitter">
			<div>
				<textarea id="cm-coffeescript" name="code" rows="5">Say "Hello CoffeeScript"</textarea>
			</div>
			<div>
				<textarea id="cm-javascript" name="code" rows="5"></textarea>
			</div>
		</div>
		<div id="output">
			<div id="compilation"></div>
			<pre id="info"></pre>
		</div>
	</div>
	<footer>
		<script src="lib/CodeMirror/lib/codemirror.js"></script>
		<script src="lib/jquery/dist/jquery.js"></script>
		<script src="lib/CodeMirror/mode/javascript/javascript.js"></script>
		<script src="lib/CodeMirror/mode/coffeescript/coffeescript.js"></script>
		<script src="lib/Touch-Splitter-jQuery/src/jquery.touchsplitter.js"></script>
		<script src="lib/coffee-script/extras/coffee-script.js"></script>
		<script src="lib/CodeMirror/addon/edit/closebrackets.js"></script>
		<script src="lib/CodeMirror/addon/selection/mark-selection.js"></script>
		<script type="text/coffeescript">
			console.log "CoffeeScript"
			window.editor = {}

			editor.outputSplitter = $("#output-splitter").verticalSplit()
			editor.codeSplitter = $("#code-splitter").horizontalSplit()

			editor.codeSplitter.on 'resize', (e) ->
				editor.coffeescript.refresh()
				editor.javascript.refresh()

			editor.coffeescript = CodeMirror.fromTextArea document.getElementById("cm-coffeescript"), {
				mode: "coffeescript"
				tabMode: "indent"
				useTabs: true
				theme: "elegant"
				styleSelectedText: true
				lineNumbers: true
				autoCloseBrackets: true
				showCursorWhenSelecting: true
				autofocus: true
				extraKeys:
					"Ctrl-Enter": (cm)->
						preview(cm)
			}
			editor.javascript = CodeMirror.fromTextArea document.getElementById("cm-javascript"), {
				mode: "javascript"
				styleSelectedText: true
				showCursorWhenSelecting: true
				cursorBlinkRate: 0
				readOnly: true
			}
			editor.compile = ->
				source = editor.coffeescript.doc.getValue()
				editor.compiledJS = ""
				try
					editor.compiledJS = CoffeeScript.compile source, bare: on
					editor.javascript.doc.setValue(editor.compiledJS)
					$('#error').hide()
					editor.errorHandler null
				catch {location, message}
					if location?
						message = "Error on line #{location.first_line + 1}: #{message}"
					$('#error').text(message).show()
					editor.errorHandler {location, message}
			editor.currentError = null
			editor.errorHandler = (err) ->
				editor.currentError.clear() if editor.currentError and editor.currentError.clear
				editor.currentError = null
				if err
					err = err.location
					if err.first_column is err.last_column and err.first_line is err.last_line
						editor.currentError = editor.coffeescript.setBookmark(
							{line:err.first_line, ch:err.first_column},
							{widget:editor.makeWidget()}
						)
					else	
						editor.currentError = editor.coffeescript.markText(
							{line:err.first_line, ch:err.first_column},
							{line:err.last_line,  ch:err.last_column},
							{className:"cm-error"}
						)
			editor.makeWidget= ->
				widget = document.createElement("span")
				widget.style.borderLeft = "1px solid rgba(255,50,50,.6)"
				widget.style.borderRight = "1px solid rgba(255,120,120,.6)"
				widget.style.marginLeft = "-1px"
				widget.style.marginRight = "-1px"
				widget
			editor.run = ->
				script = document.createElement("script");
				script.innerHTML = editor.compiledJS
				$("#compilation").html("").append(script)
			editor.store = ->
				if Storage?
					sessionStorage.code = editor.coffeescript.getValue()
				else
					return false
			editor.storeLibraries = ->
				if Storage?
					localStorage.libraries = editor.getDistinctArray(editor.libraries).join ','
				else
					return false
			editor.refresh = ->
				location.href = location.href.replace /#.+$/, ''
			editor.coffeescript.on "change", ->
				editor.compile()
				editor.store()
			editor.libraries = []
			editor.scriptsLoading = 0
			editor.que = ()->
				editor.scriptsLoading++
				return (data)->
					editor.scriptsLoading--
					if editor.scriptsLoading is 0
						editor.run()

			editor.addLibrary = (siblingelement)->
				urlInput = $(siblingelement.parentNode).find("input")
				url = urlInput.val()
				urlInput.val("")
				editor.addLibraryFromUrl url

			editor.addLibraryFromUrl = (url)->
				editor.loadLibrary(url)
				editor.libraries.push url
				editor.libraries = editor.getDistinctArray editor.libraries
				localStorage.libraries = editor.libraries.join(',')
				libraryli = document.createElement 'li'
				hex = Math.random() * 0xFFFFFF
				libraryli.innerHTML = """<a id="lib#{hex}" href="#{url}">#{(url.match /([^\/]+\.js$)/)[1]}</a><span>-</span>"""
				$('.dropdown ul').append(libraryli).find('span').click editor.remLibrary

			editor.remLibrary = (event)->
				liUrl = $(event.target).parent()
				url = liUrl.find('a').attr('href')
				liUrl.remove()
				newLibs = []
				for lib in editor.libraries
					if lib isnt url
						newLibs.push lib
				editor.libraries = newLibs
				editor.storeLibraries()
				editor.refresh()

			editor.loadLibrary = (libURL) ->
				Say "<span><a href='#{libURL}'>#{libURL}</a> loaded.</span>"
				$.ajax {
					url: libURL
					dataType: "script"
					success: editor.que()
				}

			editor.share = ->
				window.prompt "Link to share: (Ctrl-C, Enter)", location.href.replace(/#.+$/, '') + '#' + encodeURIComponent editor.coffeescript.getValue()
			editor.getDistinctArray = (arr) ->
				dups = {}
				arr.filter (el)->
					hash = el.valueOf()
					isDup = dups[hash]
					dups[hash] = true
					return !isDup

			if Storage? and sessionStorage.code
				editor.coffeescript.setValue sessionStorage.code

			if Storage? and localStorage.libraries
				editor.libraries = editor.getDistinctArray localStorage.libraries.split ','
				for libURL in editor.libraries
					editor.addLibraryFromUrl(libURL)

			if location.href.match /#.+/
				editor.coffeescript.setValue decodeURIComponent location.href.replace /^.+?#/, ''
				editor.refresh() if editor.store()
			editor.compile()
		</script>
		<script>
			Say = function(o){
				console.log(o)
				$("#info").append(o.toString() + "\n")
			}
		</script>
	</footer>
</body>
</html>
